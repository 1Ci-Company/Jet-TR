#If Server Or ExternalConnection Then

#Region EventHandlers

Procedure Filling(FillingData, FillingText, StandardProcessing)
	
	ObjectFillingJet.FillDocument(ThisObject, FillingData);
	
	If TypeOf(FillingData) = Type("DocumentRef.SupplierInvoice") Then
		
		Query = New Query;
		Query.Text = 
		"SELECT
		|	SupplierInvoice.Ref AS Ref,
		|	SupplierInvoice.Supplier AS Supplier,
		|	SupplierInvoice.Currency AS Currency,
		|	SupplierInvoice.ExchangeRate AS ExchangeRate,
		|	SupplierInvoice.Multiplier AS Multiplier,
		|	SupplierInvoice.Total AS Total
		|INTO DocumentHeader
		|FROM
		|	Document.SupplierInvoice AS SupplierInvoice
		|WHERE
		|	SupplierInvoice.Ref = &Ref
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|SELECT TOP 1
		|	CashAccounts.Ref AS Ref,
		|	CashAccounts.Currency AS Currency
		|INTO CashAccount
		|FROM
		|	Catalog.CashAccounts AS CashAccounts
		|		INNER JOIN DocumentHeader AS DocumentHeader
		|		ON CashAccounts.Currency = DocumentHeader.Currency
		|WHERE
		|	NOT CashAccounts.DeletionMark
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|SELECT
		|	VALUE(Enum.CashVoucherOperations.Supplier) AS Operation,
		|	DocumentHeader.Supplier AS Counterparty,
		|	ISNULL(CashAccount.Ref, VALUE(Catalog.CashAccounts.EmptyRef)) AS CashAccount,
		|	DocumentHeader.Currency AS Currency,
		|	DocumentHeader.ExchangeRate AS ExchangeRate,
		|	DocumentHeader.Multiplier AS Multiplier,
		|	DocumentHeader.Ref AS Document,
		|	DocumentHeader.Total AS PaymentAmount,
		|	CASE
		|		WHEN DocumentHeader.Multiplier = 0
		|			THEN 0
		|		ELSE CAST(DocumentHeader.Total * DocumentHeader.ExchangeRate / DocumentHeader.Multiplier AS NUMBER(15, 2))
		|	END AS Amount
		|FROM
		|	DocumentHeader AS DocumentHeader
		|		LEFT JOIN CashAccount AS CashAccount
		|		ON DocumentHeader.Currency = CashAccount.Currency";
		
		Query.SetParameter("Ref", FillingData);
		QueryResult = Query.Execute();
		
		If Not QueryResult.IsEmpty() Then
			
			Selection = QueryResult.Select();
			Selection.Next();
			
			FillPropertyValues(ThisObject, Selection);
			
			PaymentDetails.Clear();
			FillPropertyValues(PaymentDetails.Add(), Selection);
			
		EndIf;
		
	EndIf;
	
EndProcedure

Procedure BeforeWrite(Cancel, WriteMode, PostingMode)
	
	If DataExchange.Load Then
		Return;
	EndIf;
	
	PaymentAmount = PaymentDetails.Total("PaymentAmount");
	
EndProcedure

Procedure Posting(Cancel, PostingMode)
	
	// Initialization of additional properties for document posting.
	PostingManagement.InitializeAdditionalPropertiesForPosting(Ref, AdditionalProperties);
	
	// Document data initialization.
	Documents.CashVoucher.InitializeDocumentData(Ref, AdditionalProperties);
	
	// Preparation of records sets.
	PostingManagement.PrepareRecordSetsForWriting(ThisObject);
	
	// Movements on the CashBalance register
	PostingManagement.ReflectCashBalance(AdditionalProperties, RegisterRecords, Cancel);
	
	// Movements on the SupplierBalance register
	PostingManagement.ReflectSupplierBalance(AdditionalProperties, RegisterRecords, Cancel);
	
	// Writing of the records sets.
	PostingManagement.WriteRecordSets(ThisObject);
	
EndProcedure

Procedure UndoPosting(Cancel)
	
	// Initialization of additional properties for document posting.
	PostingManagement.InitializeAdditionalPropertiesForPosting(Ref, AdditionalProperties);
	
	// Preparation of records sets.
	PostingManagement.PrepareRecordSetsForWriting(ThisObject);
	
	// Writing of the records sets.
	PostingManagement.WriteRecordSets(ThisObject);
	
EndProcedure

Procedure FillCheckProcessing(Cancel, CheckedAttributes)
	
	If Operation = Enums.CashVoucherOperations.Other Then
		CheckedAttributes.Delete(CheckedAttributes.Find("Counterparty"));
	EndIf;
	
EndProcedure

#EndRegion

#EndIf